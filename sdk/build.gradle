plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'signing'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

tasks.create("javadocJar", Jar) {
    dependsOn("dokka")
    archivesBaseName = ""
    archiveClassifier.set("javadocs")
    from("$buildDir/javadoc")
}

tasks.create("sourcesJar", Jar) {
    archivesBaseName = ""
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact "$buildDir/outputs/aar/sdk-release.aar"
            artifact sourcesJar
            artifact javadocJar

            groupId = "io.microcms"
            artifactId = "android-sdk"
            version = "1.0.2"

            pom {
                name = 'io.microcms'
                description = 'microCMS Android SDK'
                url = 'https://github.com/wantainc/microcms-android-sdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'shoma2da'
                        name = 'microCMS Development Team'
                        email = 'dev@microcms.io'
                    }
                }
                scm {
                    connection = 'git@github.com:wantainc/microcms-android-sdk.git'
                    developerConnection = 'https://github.com/wantainc/microcms-android-sdk.git'
                    url = 'https://github.com/wantainc/microcms-android-sdk'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/content/repositories/releases/"
            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }
    }
}

signing {
    println(publishing.publications.mavenJava)
    sign publishing.publications.mavenJava
}